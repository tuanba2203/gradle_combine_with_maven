group 'jp.btuan.express'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    // Load dependencies from old pom.xml file
    def pomXml = new XmlSlurper().parse('pom.xml')
    def pomDependencies = pomXml.dependencies.dependency
    pomDependencies.each { dependency ->
        // Get Dependency version
        def dependencyVersion = "${dependency.version}"
        if (dependencyVersion.matches("^\\\$\\{\\S*\\}\$")){
            dependencyVersion = dependencyVersion.replaceAll("[\\\$\\{\\}]", "");
            dependencyVersion = pomXml.properties."${dependencyVersion}"
        }else{
            dependencyVersion = "${dependency.version}"
        }
        def dependencySpec = "${dependency.groupId}:${dependency.artifactId}:${dependencyVersion}"
        println dependencySpec
        switch (dependency.scope){
            case 'test':
                dependencies.add 'testCompile', dependencySpec
                break;
            case 'runtime':
                dependencies.add 'runtime', dependencySpec
                break;
            default:
                dependencies.add 'compile', dependencySpec
                break;
        }
    }

    // Define dependencies with Gradle syntax
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}